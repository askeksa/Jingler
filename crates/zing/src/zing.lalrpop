
use std::f64;
use std::str::FromStr;

use lalrpop_util::ParseError;

use crate::ast::*;

grammar;

pub Program: Program<'input> = {
	<parameters:Parameter*> <procedures:Procedure*> => Program {<>},
};

Parameter: Parameter<'input> = {
	"parameter" <name:Id> <min:Num> "to" <max:Num> <default:ParameterDefault> => Parameter {<>},
};

ParameterDefault: Option<f64> = {
	=> None,
	"=" <value:Num> => Some(value),
};

Procedure: Procedure<'input> = {
	<context:Context> <kind:ProcedureKind> <channels:MidiParam*> <name:Id> <inputs:Pattern> "->" <outputs:Pattern> <body:Statement*>
		=> Procedure {<>},
};

Context: Context = {
	=> Context::Universal,
	"global" => Context::Global,
	"note" => Context::Note,
}

ProcedureKind: ProcedureKind = {
	"module" => ProcedureKind::Module,
	"function" => ProcedureKind::Function,
	"instrument" => ProcedureKind::Instrument,
}

Pattern: Pattern<'input> = {
	<before:@L> <items:PatternItems> <after:@R> => Pattern {<>},
};

PatternOptParen: Pattern<'input> = {
	<before:@L> <items:PatternItemsOptParen> <after:@R> => Pattern {<>},
};

PatternItems: Vec<PatternItem<'input>> = {
	<Vec<PatternItem>>,
	"(" <Comma<PatternItem>> ")",
};

PatternItemsOptParen: Vec<PatternItem<'input>> = {
	<Comma<PatternItem>>,
	"(" <Comma<PatternItem>> ")",
};

PatternItem: PatternItem<'input> = {
	<name:Id> => PatternItem {
		name,
		item_type: Type::default(),
	},
	<name:Id> <item_type:ExplicitType> => PatternItem {<>},
};

ExplicitType: Type = {
	":" <scope:Scope?> <width:Width?> <value_type:ValueType?> => Type {<>},
};

Scope: Scope = {
	"static" => Scope::Static,
	"dynamic" => Scope::Dynamic,
};

Width: Width = {
	"mono" => Width::Mono,
	"stereo" => Width::Stereo,
	"generic" => Width::Generic,
};

ValueType: ValueType = {
	"number" => ValueType::Number,
	"bool" => ValueType::Bool,
	"buffer" => ValueType::Buffer,
}

Statement: Statement<'input> = {
	<node:PatternOptParen> "=" <exp:Expression> ";" => Statement::Assign {<>},
};

Expression: Expression<'input> = {
	OrExpression,
	<condition:Box<OrExpression>> "?" <then:Box<Expression>> ":" <otherwise:Box<Expression>>
		=> Expression::Conditional {<>},
	<before:@L> "for" <name:Id> "to" <count:Box<Expression>> <combinator:Id> <body:Box<Expression>>
		=> Expression::For {<>},
};

OrExpression = BinOpLevel<OrOp, XorExpression>;

OrOp: BinOpKind = "|" => BinOpKind::Or;

XorExpression = BinOpLevel<XorOp, AndExpression>;

XorOp: BinOpKind = "^" => BinOpKind::Xor;

AndExpression = BinOpLevel<AndOp, CompareExpression>;

AndOp: BinOpKind = "&" => BinOpKind::And;

CompareExpression = BinOpLevel<CompareOp, AdditiveExpression>;

CompareOp: BinOpKind = {
	"==" => BinOpKind::Eq,
	"!=" => BinOpKind::Neq,
	"<" => BinOpKind::Less,
	"<=" => BinOpKind::LessEq,
	">" => BinOpKind::Greater,
	">=" => BinOpKind::GreaterEq,
};

AdditiveExpression = BinOpLevel<AdditiveOp, MultiplicativeExpression>;

AdditiveOp: BinOpKind = {
	"+" => BinOpKind::Add,
	"-" => BinOpKind::Sub,
};

MultiplicativeExpression = BinOpLevel<MultiplicativeOp, UnaryExpression>;

MultiplicativeOp: BinOpKind = {
	"*" => BinOpKind::Mul,
	"/" => BinOpKind::Div,
};

UnaryExpression: Expression<'input> = {
	<op:UnOp<UnaryOp>> <exp:Box<PrimaryExpression>> => Expression::UnOp {<>},
	PrimaryExpression,
};

UnaryOp: UnOpKind = {
	"-" => UnOpKind::Neg,
	"!" => UnOpKind::Not,
};

PrimaryExpression: Expression<'input> = {
	<before:@L> <value:Num> <after:@R> => Expression::Number {<>},
	<before:@L> <value:Bool> => Expression::Bool {<>},
	<name:Id> => Expression::Variable {<>},
	<before:@L> <channels:MidiArg*> <name:Id> "(" <args:Comma<Expression>> ")" <after:@R> => Expression::Call {<>},
	<before:@L> "(" <elements:Comma<Expression>> ")" <after:@R> => Expression::Tuple {<>},
	<before:@L> "[" <left:Box<Expression>> "," <right:Box<Expression>> "]" <after:@R> => Expression::Merge {<>},
	<exp:Box<PrimaryExpression>> "." <name:Id> => Expression::Property {<>},
	<exp:Box<PrimaryExpression>> "." <index:Uint> <after:@R> => Expression::TupleIndex {<>},
	<exp:Box<PrimaryExpression>> "[" <index:Box<Expression>> "]" <after:@R> => Expression::BufferIndex {<>},
};

MidiParam: Id<'input> = <name:Id> "::" => name;

MidiArg: MidiChannel<'input> = {
	<value:Uint> "::" => MidiChannel::Value { channel: value as usize },
	<name:Id> "::" => MidiChannel::Named { name },
};

Num: f64 = {
	<pos:@L> <i:Uint> =>? {
		let n = i as f64;
		if n as u64 == i {
			Ok(n)
		} else {
			Err(ParseError::InvalidToken { location: pos })
		}
	},
	DecimalNum,
	HexadecimalNum,
	"inf" => f64::INFINITY,
};

Bool: bool = {
	"false" => false,
	"true" => true,
};

Id: Id<'input> = {
	<before:@L> <text:r"[_a-zA-Z][_a-zA-Z0-9]*"> => Id {<>},
};

Uint: u64 = <pos:@L> <s:r"0|[1-9][0-9]*"> =>? {
	u64::from_str(s).map_err(|e| ParseError::InvalidToken { location: pos })
};

DecimalNum: f64 = <pos:@L> <s:r"(0|[1-9][0-9]*)[.][0-9]+"> =>? {
	f64::from_str(s).map_err(|e| ParseError::InvalidToken { location: pos })
};

HexadecimalNum: f64 = <pos:@L> <s:r"0x[0-9a-fA-F]+([.][0-9a-fA-F]*)?"> =>? {
	let mut n = 0.0;
	let mut f = 1.0;
	let mut ff = 1.0;
	for c in s.bytes().skip(2) {
		if c == b'.' {
			ff = 1.0 / 16.0;
		} else {
			let digit = match c {
				(b'0' ..= b'9') => c - b'0',
				(b'a' ..= b'f') => 10 + c - b'a',
				(b'A' ..= b'F') => 10 + c - b'A',
				_ => panic!(),
			} as f64;
			n *= 16.0;
			let new_n = n + digit;
			if new_n - n != digit {
				return Err(ParseError::InvalidToken { location: pos });
			}
			n = new_n;
			f *= ff;
		}
	}
	Ok(n * f)
};

Comma<T>: Vec<T> = <v:(<T> ",")*> <e:T?> => v.into_iter().chain(e).collect();

Box<T>: Box<T> = <data:T> => Box::new(data);

Vec<T>: Vec<T> = <item:T> => vec![item];

UnOp<Op>: UnOp = {
	<before:@L> <kind:Op> => UnOp {<>}
}

BinOp<Op>: BinOp = {
	<before:@L> <kind:Op> => BinOp {<>}
}

BinOpLevel<Op, NextLevel>: Expression<'input> = {
	<left:Box<BinOpLevel<Op, NextLevel>>> <op:BinOp<Op>> <right:Box<NextLevel>>
		=> Expression::BinOp {<>},
	NextLevel
}
